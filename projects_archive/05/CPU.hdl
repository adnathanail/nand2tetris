// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in=instruction[15], out=aInstruction);
    Not(in=aInstruction, out=cInstruction);

    Mux16(a=aluOut, b[0..14]=instruction[0..14], a[15]=false, sel=aInstruction, out=aIn);
    Or(a=instruction[5], b=aInstruction, out=loadA); // if A-Instruction or d1
    ARegister(in=aIn, load=loadA, out=aReg, out[0..14]=addressM);

    And(a=cInstruction, b=instruction[4], out=loadD); // If C-Instruction and d2
    DRegister(in=aluOut, load=loadD, out=dReg);

    Mux16(a=aReg, b=inM, sel=instruction[12], out=op2); // Switches between A and M dependant on a. Producing second operand for ALU

    ALU(x=dReg, y=op2, zx=instruction[11], nx=instruction[10], zy=instruction[9],
    ny=instruction[8], f=instruction[7], no=instruction[6], out=aluOut, out=outM, zr=eq, ng=lt);

    And(a=cInstruction, b=instruction[3], out=writeM);

    And(a=eq, b=instruction[1], out=jeq);
    And(a=lt, b=instruction[2], out=jlt);
    Or(a=eq, b=lt, out=lteq);
    Not(in=lteq, out=gt);
    And(a=gt, b=instruction[0], out=jgt);
    Or(a=jeq, b=jlt, out=jlteq);
    Or(a=jlteq, b=jgt, out=j);
    And(a=j, b=cInstruction, out=loadPC);
    PC(in=aReg, load=loadPC, inc=true, reset=reset, out[0..14]=pc);

}