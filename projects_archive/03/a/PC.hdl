// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // If elif etc. implemented by checking last condition first and overwriting if an earlier is found
    Inc16(in=regOut, out=regOutInc); // Creates incremented value if needed
    Mux16(a=regOut, b=regOutInc, sel=inc, out=inced); // Switches to incremented value if needed
    Mux16(a=inced, b=in, sel=load, out=loaded); // Switches to loaded value if needed
    Mux16(a=loaded, b[0..15]=false, sel=reset, out=resetted); // Resets if needed
    Register(in=resetted, load=true, out=regOut, out=out); // Puts final value in register
}
