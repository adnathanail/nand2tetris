// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array twoToThe;

    /** Initializes the library. */
    function void init() {
        var int i, n;

        let twoToThe = Array.new(16);

        let i = 0;
        let n = 1;
        while (i < 16) {
            let twoToThe[i] = n;
            let i = i + 1;
            let n = n + n;
        }
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        } else {
            return x;
        }
    }

    function boolean bit(int x, int j) {
        return ~((twoToThe[j] & x) = 0);
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int sum, xShifted, i;

        let sum = 0;
        let xShifted = x;
        let i = 0;

        while(i < 16) {
            if (Math.bit(y, i)) {
                let sum = sum + xShifted;
            }
            let xShifted = xShifted + xShifted;
            let i = i + 1;
        }

        return sum;
    }
    
    function int pow(int a, int b) {
        var int out;

        let out = 1;
        while (b > 0) {
            let out = out * a;
            let b = b - 1;
        }

        return out;
    }

    function int pos_divide(int x, int y) {
        var int q, tem;

        if ((y > x) | (y < 0)) {
            return 0;
        }

        let q = Math.pos_divide(x, y + y);
        let tem = q * y;
        if ((x - (tem + tem)) < y) {
            return q + q;
        } else {
            return (q + q) + 1;
        }
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var boolean negate_output;

        // normalise inputs to be positive, and determine whether we will later negate the output
        let negate_output = false;
        if (x < 0) {
            let x = -x;
            let negate_output = true;
        }
        if (y < 0) {
            let y = -y;
            let negate_output = ~negate_output;
        }

        // negate output if needed
        if (negate_output) {
            return - Math.pos_divide(x, y);
        } else {
            return Math.pos_divide(x, y);
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int min, max, mdpt, mdpt_sq;

        let min = 1;
        if (x < 10) {
            let max = x;
        } else {
            let max = x / 2;
        }
        let mdpt = (max + min) / 2;

        while(~((max - min) = 1)) {
            let mdpt_sq = mdpt * mdpt;
            if (mdpt_sq = x) {
                return mdpt;
            }
            if ((mdpt_sq > 0) & (mdpt_sq < x)) {
                let min = mdpt;
            } else {
                let max = mdpt;
            }
            let mdpt = (max + min)/2;
        }

        return min;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        } else {
            return b;
        }
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        } else {
            return b;
        }
    }
}
